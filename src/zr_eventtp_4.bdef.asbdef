managed implementation in class zbp_r_eventtp_4 unique;
strict ( 2 );

define behavior for ZR_EVENTTP_4 alias Event
persistent table zevent_4
lock master
authorization master ( instance )
//etag master <field_name>
{
  create ( authorization : global );
  delete;
  update;

  association _Registrations { create; }

  field ( readonly, numbering : managed ) EventUuid;
  field ( readonly ) EventID, Status;
  field ( mandatory : create, mandatory : update ) Title, Location, StartDate, EndDate, MaxParticipants, Description;

  determination SetEventStatus on modify { create; }
  determination GenerateEventId on modify { create; }

  validation ValidateDates on save { create; }
  validation ValidateStartDate on save { create; }

  action OpenEvent result [1] $self;
  action CloseEvent result [1] $self;

  mapping for zevent_4 corresponding
    {
      EventUuid = event_uuid;
      EventId = event_id;
      EndDate = end_date;
      Description = description;
      MaxParticipants = max_participants;
      Location = location;
      Title = title;
      Status = status;
      StartDate = start_date;
      CreatedBy = created_by;
      CreatedAt = created_at;
      LastChangedAt = last_changed_at;
      LastChangedBy = last_changed_by;
    }
}

define behavior for ZR_Registration_4 alias Registrations
persistent table zregistration_4
lock dependent by _Event
authorization dependent by _Event
//etag master <field_name>
{
  delete;
  update;

  association _Event;

  validation MaxParticipantsNotReached on save { create; }

  determination SetRegistrationstatus on modify { create; }
  determination GenerateRegistrationId on modify { create; }

  field ( readonly, numbering : managed ) RegistrationUuid;
  field ( readonly ) RegistrationId, EventUuid, Status;
  field ( mandatory : create, mandatory : update ) ParticipantUuid;

  mapping for zregistration_4 corresponding
    {
      RegistrationUuid = registration_uuid;
      RegistrationId = registration_id;
      EventUuid = event_uuid;
      ParticipantUuid = participant_uuid;
      Remarks = remarks;
      Status = status;
      CreatedBy = created_by;
      CreatedAt = created_at;
      LastChangedAt = last_changed_at;
      LastChangedBy = last_changed_by;
    }
}